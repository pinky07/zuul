buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

plugins {
    id 'org.springframework.boot' version '1.5.6.RELEASE'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'docker'

group = 'com.gft'
version = '0.0.1-SNAPSHOT'
description = 'GFT-Zuul'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

ext {
    gVersion = '4.1'
    springCloudVersion = 'Dalston.SR2'
}

dependencies {
    // Web
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-actuator')

	// Security
    compile('org.springframework.boot:spring-boot-starter-security')
    
	// Eureka Client
    compile('org.springframework.cloud:spring-cloud-starter-eureka')
    
	// Zuul
    compile('org.springframework.cloud:spring-cloud-starter-zuul')
    compile('com.netflix.netflix-commons:netflix-commons-util')
    
	// Test
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

task buildDocker(type: Docker, dependsOn: build) {
    tag = "com.gft.zuul"
    tagVersion = 'latest'
    baseImage = 'openjdk:8'
    push = false // Would push to a Docker Hub account
    applicationName = jar.baseName
    addFile {
        from jar
        rename {'app.jar'}
    }
    entryPoint(['java', '-Djava.security.egd=file:/dev/./urandom', '-jar', '/app.jar'])
}

// Avoids distribution version to be changed from ALL to BIN in gradle-wrapper.properties
wrapper.gradleVersion = gVersion
wrapper.distributionType = Wrapper.DistributionType.ALL